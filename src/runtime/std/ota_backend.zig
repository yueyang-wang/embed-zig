const std = @import("std");
const embed = @import("../../mod.zig");

pub const OtaBackend = struct {
    file: ?std.fs.File = null,
    expected_size: u32 = 0,
    written_size: u32 = 0,
    confirmed: bool = true,
    stage_path: []const u8 = ".runtime_ota_stage.bin",
    final_path: []const u8 = ".runtime_ota_applied.bin",
    confirm_path: []const u8 = ".runtime_ota_confirmed",

    pub fn init() embed.runtime.ota_backend.Error!@This() {
        return .{};
    }

    pub fn begin(self: *@This(), image_size: u32) embed.runtime.ota_backend.Error!void {
        self.abort();
        self.expected_size = image_size;
        self.written_size = 0;
        self.file = createFileAt(self.stage_path) catch return error.OpenFailed;
    }

    pub fn write(self: *@This(), chunk: []const u8) embed.runtime.ota_backend.Error!void {
        var f = self.file orelse return error.WriteFailed;
        f.writeAll(chunk) catch return error.WriteFailed;
        self.file = f;

        const next = @as(u64, self.written_size) + chunk.len;
        self.written_size = if (next > std.math.maxInt(u32)) std.math.maxInt(u32) else @intCast(next);
    }

    pub fn finalize(self: *@This()) embed.runtime.ota_backend.Error!void {
        if (self.file) |f| {
            f.close();
            self.file = null;
        } else {
            return error.FinalizeFailed;
        }

        if (self.expected_size != 0 and self.written_size != self.expected_size) {
            return error.FinalizeFailed;
        }

        deleteFileAt(self.final_path);
        renameAt(self.stage_path, self.final_path) catch return error.FinalizeFailed;
        deleteFileAt(self.confirm_path);
        self.confirmed = false;
    }

    pub fn abort(self: *@This()) void {
        if (self.file) |f| {
            f.close();
            self.file = null;
        }
        deleteFileAt(self.stage_path);
    }

    pub fn confirm(self: *@This()) embed.runtime.ota_backend.Error!void {
        if (self.confirmed) return;
        _ = createFileAt(self.confirm_path) catch return error.ConfirmFailed;
        self.confirmed = true;
    }

    pub fn rollback(self: *@This()) embed.runtime.ota_backend.Error!void {
        deleteFileAt(self.final_path);
        deleteFileAt(self.confirm_path);
        self.confirmed = true;
    }

    pub fn getState(self: *@This()) embed.runtime.ota_backend.State {
        if (!fileExists(self.final_path)) return .unknown;
        if (self.confirmed or fileExists(self.confirm_path)) return .valid;
        return .pending_verify;
    }

    fn fileExists(path: []const u8) bool {
        if (std.fs.path.isAbsolute(path)) {
            std.fs.accessAbsolute(path, .{}) catch return false;
            return true;
        }
        std.fs.cwd().access(path, .{}) catch return false;
        return true;
    }

    fn createFileAt(path: []const u8) !std.fs.File {
        if (std.fs.path.isAbsolute(path)) {
            return std.fs.createFileAbsolute(path, .{ .read = true, .truncate = true });
        }
        return std.fs.cwd().createFile(path, .{ .read = true, .truncate = true });
    }

    fn renameAt(old_path: []const u8, new_path: []const u8) !void {
        const old_abs = std.fs.path.isAbsolute(old_path);
        const new_abs = std.fs.path.isAbsolute(new_path);
        if (old_abs != new_abs) return error.InvalidPath;

        if (old_abs) {
            return std.fs.renameAbsolute(old_path, new_path);
        }
        return std.fs.cwd().rename(old_path, new_path);
    }

    fn deleteFileAt(path: []const u8) void {
        if (std.fs.path.isAbsolute(path)) {
            std.fs.deleteFileAbsolute(path) catch {};
            return;
        }
        std.fs.cwd().deleteFile(path) catch {};
    }
};
